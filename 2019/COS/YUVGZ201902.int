ROUTINE YUVGZ201902 [Type=INT]
YUVGZ201902	;https://adventofcode.com/2019/day/2;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
1,0,0,3,1,1,2,3,1,3,4,3,1,5,0,3,2,1,10,19,1,6,19,23,1,13,23,27,1,6,27,31,1,31,10,35,1,35,6,39,1,39,13,43,2,10,43,47,1,47,6,51,2,6,51,55,1,5,55,59,2,13,59,63,2,63,9,67,1,5,67,71,2,13,71,75,1,75,5,79,1,10,79,83,2,6,83,87,2,13,87,91,1,9,91,95,1,9,95,99,2,99,9,103,1,5,103,107,2,9,107,111,1,5,111,115,1,115,2,119,1,9,119,0,99,2,0,14,0
	*/
	/*
On the way to your gravity assist around the Moon, your ship computer beeps angrily about a "1202 program alarm". On the radio, an Elf is already explaining how to handle the situation: "Don't worry, that's perfectly norma--" The ship computer bursts into flames.

You notify the Elves that the computer's magic smoke seems to have escaped. "That computer ran Intcode programs like the gravity assist program it was working on; surely there are enough spare parts up there to build a new Intcode computer!"

An Intcode program is a list of integers separated by commas (like 1,0,0,3,99). To run one, start by looking at the first integer (called position 0). Here, you will find an opcode - either 1, 2, or 99. The opcode indicates what to do; for example, 99 means that the program is finished and should immediately halt. Encountering an unknown opcode means something went wrong.

Opcode 1 adds together numbers read from two positions and stores the result in a third position. The three integers immediately after the opcode tell you these three positions - the first two indicate the positions from which you should read the input values, and the third indicates the position at which the output should be stored.

For example, if your Intcode computer encounters 1,10,20,30, it should read the values at positions 10 and 20, add those values, and then overwrite the value at position 30 with their sum.

Opcode 2 works exactly like opcode 1, except it multiplies the two inputs instead of adding them. Again, the three integers after the opcode indicate where the inputs and outputs are, not their values.

Once you're done processing an opcode, move to the next one by stepping forward 4 positions.

For example, suppose you have the following program:

1,9,10,3,2,3,11,0,99,30,40,50
For the purposes of illustration, here is the same program split into multiple lines:

1,9,10,3,
2,3,11,0,
99,
30,40,50
The first four integers, 1,9,10,3, are at positions 0, 1, 2, and 3. Together, they represent the first opcode (1, addition), the positions of the two inputs (9 and 10), and the position of the output (3). To handle this opcode, you first need to get the values at the input positions: position 9 contains 30, and position 10 contains 40. Add these numbers together to get 70. Then, store this value at the output position; here, the output position (3) is at position 3, so it overwrites itself. Afterward, the program looks like this:

1,9,10,70,
2,3,11,0,
99,
30,40,50
Step forward 4 positions to reach the next opcode, 2. This opcode works just like the previous, but it multiplies instead of adding. The inputs are at positions 3 and 11; these positions contain 70 and 50 respectively. Multiplying these produces 3500; this is stored at position 0:

3500,9,10,70,
2,3,11,0,
99,
30,40,50
Stepping forward 4 more positions arrives at opcode 99, halting the program.

Here are the initial and final states of a few more small programs:

1,0,0,0,99 becomes 2,0,0,0,99 (1 + 1 = 2).
2,3,0,3,99 becomes 2,3,0,6,99 (3 * 2 = 6).
2,4,4,5,99,0 becomes 2,4,4,5,99,9801 (99 * 99 = 9801).
1,1,1,4,99,5,6,0,99 becomes 30,1,1,4,2,5,6,0,99.
Once you have a working computer, the first step is to restore the gravity assist program (your puzzle input) to the "1202 program alarm" state it had just before the last computer caught fire. To do this, before running the program, replace position 1 with the value 12 and replace position 2 with the value 2. What value is left at position 0 after the program halts?
	*/
Part1(Filename,ModuleData) PUBLIC
{
	set Filename=$get(Filename)
	if $data(ModuleData)=1,$length($get(ModuleData)) set ModuleData(1)=ModuleData
	if $data(ModuleData)<10 {
		if '$length(Filename) {
			write !,"Enter file location: " read Filename
		}
		if '$length(Filename) {
			write !,"No data enterd, so bye bye for now"
			quit 0
		}
		if '$$LoadFile(Filename,.ModuleData) {
			write !,"Can't open file "_Filename_" for reading, bye bye"
			quit 0
		}
	}
	if '$data(ModuleData) {
		write !,"Data wan't found in file "_Filename
		quit 0
	}
	if ($order(ModuleData(""))'=1)||($order(ModuleData(""),-1)'=1) {
		write !,"Wrong data in file "_Filename
		quit 0
	}
	set Data=ModuleData(1)
	if (Data_",")'?1.(1.n1",") {
		write !,"Wrong data in file "_Filename
		quit 0
	}
	w !,Data,!
	set $piece(Data,",",2)=12,$piece(Data,",",3)=2
	set OpCodeIP=0,ERR=""
	for  {
		set OpCode=$piece(Data,",",OpCodeIP+1)
		if OpCode'=1,OpCode'=2,OpCode'=99 {
			set ERR="Wrong OpCode at position "_OpCodeIP_" in file "_Filename_" ("_OpCode_")"
			quit
		}
		quit:OpCode=99
		set Val1Position=$piece(Data,",",OpCodeIP+2)
		set Val2Position=$piece(Data,",",OpCodeIP+3)
		set AnsPosition=$piece(Data,",",OpCodeIP+4)
		set Val1=$piece(Data,",",Val1Position+1)
		set Val2=$piece(Data,",",Val2Position+1)
		if OpCode=1 set Ans=Val1+Val2
		if OpCode=2 set Ans=Val1*Val2
		set $piece(Data,",",AnsPosition+1)=Ans
		set OpCodeIP=OpCodeIP+4
	}
	if $length(ERR) {
		write !,ERR
		quit 0
	}
	w !,Data,!!
	quit $piece(Data,",")
}


	/*
"Good, the new computer seems to be working correctly! Keep it nearby during this mission - you'll probably use it again. Real Intcode computers support many more features than your new one, but we'll let you know what they are as you need them."

"However, your current priority should be to complete your gravity assist around the Moon. For this mission to succeed, we should settle on some terminology for the parts you've already built."

Intcode programs are given as a list of integers; these values are used as the initial state for the computer's memory. When you run an Intcode program, make sure to start by initializing memory to the program's values. A position in memory is called an address (for example, the first value in memory is at "address 0").

Opcodes (like 1, 2, or 99) mark the beginning of an instruction. The values used immediately after an opcode, if any, are called the instruction's parameters. For example, in the instruction 1,2,3,4, 1 is the opcode; 2, 3, and 4 are the parameters. The instruction 99 contains only an opcode and has no parameters.

The address of the current instruction is called the instruction pointer; it starts at 0. After an instruction finishes, the instruction pointer increases by the number of values in the instruction; until you add more instructions to the computer, this is always 4 (1 opcode + 3 parameters) for the add and multiply instructions. (The halt instruction would increase the instruction pointer by 1, but it halts the program instead.)

"With terminology out of the way, we're ready to proceed. To complete the gravity assist, you need to determine what pair of inputs produces the output 19690720."

The inputs should still be provided to the program by replacing the values at addresses 1 and 2, just like before. In this program, the value placed in address 1 is called the noun, and the value placed in address 2 is called the verb. Each of the two input values will be between 0 and 99, inclusive.

Once the program has halted, its output is available at address 0, also just like before. Each time you try a pair of inputs, make sure you first reset the computer's memory to the values in the program (your puzzle input) - in other words, don't reuse memory from a previous attempt.

Find the input noun and verb that cause the program to produce the output 19690720. What is 100 * noun + verb? (For example, if noun=12 and verb=2, the answer would be 1202.)
	*/
Part2(Filename,ModuleData,TargetValue) PUBLIC
{
	set Filename=$get(Filename)
	if $get(TargetValue)'?1.N {
		write !,"Wrong target value ("_$get(TargetValue)_")"
		quit 0
	}
	if $data(ModuleData)=1,$length($get(ModuleData)) set ModuleData(1)=ModuleData
	if $data(ModuleData)<10 {
		if '$length(Filename) {
			write !,"Enter file location: " read Filename
		}
		if '$length(Filename) {
			write !,"No data enterd, so bye bye for now"
			quit 0
		}
		if '$$LoadFile(Filename,.ModuleData) {
			write !,"Can't open file "_Filename_" for reading, bye bye"
			quit 0
		}
	}
	if '$data(ModuleData) {
		write !,"Data wan't found in file "_Filename
		quit 0
	}
	if ($order(ModuleData(""))'=1)||($order(ModuleData(""),-1)'=1) {
		write !,"Wrong data in file "_Filename
		quit 0
	}
	set Data=ModuleData(1)
	if (Data_",")'?1.(1.n1",") {
		write !,"Wrong data in file "_Filename
		quit 0
	}
	w !,Data,!
	set OriginalData=Data
	set ERR=""
	for Noun=0:1:99 {
		for Verb=0:1:99 {
			set Data=OriginalData
			set $piece(Data,",",2)=Noun,$piece(Data,",",3)=Verb
			set OpCodeIP=0
			for  {
				set OpCode=$piece(Data,",",OpCodeIP+1)
				if OpCode'=1,OpCode'=2,OpCode'=99 {
					set ERR="Wrong OpCode at position "_OpCodeIP_" in file "_Filename_" ("_OpCode_")"
					quit
				}
				quit:OpCode=99
				set Val1Position=$piece(Data,",",OpCodeIP+2)
				set Val2Position=$piece(Data,",",OpCodeIP+3)
				set AnsPosition=$piece(Data,",",OpCodeIP+4)
				set Val1=$piece(Data,",",Val1Position+1)
				set Val2=$piece(Data,",",Val2Position+1)
				if OpCode=1 set Ans=Val1+Val2
				if OpCode=2 set Ans=Val1*Val2
				set $piece(Data,",",AnsPosition+1)=Ans
				set OpCodeIP=OpCodeIP+4
			}
			quit:$length(ERR)
			quit:$piece(Data,",")=TargetValue
		}
		quit:$length(ERR)
		quit:$piece(Data,",")=TargetValue
	}
	if $length(ERR) {
		write !,ERR
		quit 0
	}
	w !,Data,!!
	set Noun=$piece(Data,",",2),Verb=$piece(Data,",",3)
	quit 100*Noun+Verb
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::2 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		if $length(Record) set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
