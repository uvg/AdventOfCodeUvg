YUVGZ201924	;https://adventofcode.com/2019/day/24;
	quit
	/*
#.#..
.....
.#.#.
.##..
.##.#
	*/
	/*
You land on Eris, your last stop before reaching Santa. As soon as you do, your sensors start picking up strange life forms moving around: Eris is infested with bugs! With an over 24-hour roundtrip for messages between you and Earth, you'll have to deal with this problem on your own.

Eris isn't a very large place; a scan of the entire area fits into a 5x5 grid (your puzzle input). The scan shows bugs (#) and empty spaces (.).

Each minute, The bugs live and die based on the number of bugs in the four adjacent tiles:

A bug dies (becoming an empty space) unless there is exactly one bug adjacent to it.
An empty space becomes infested with a bug if exactly one or two bugs are adjacent to it.
Otherwise, a bug or empty space remains the same. (Tiles on the edges of the grid have fewer than four adjacent tiles; the missing tiles count as empty space.) This process happens in every location simultaneously; that is, within the same minute, the number of adjacent bugs is counted for every tile first, and then the tiles are updated.

Here are the first few minutes of an example scenario:

Initial state:
....#
#..#.
#..##
..#..
#....

After 1 minute:
#..#.
####.
###.#
##.##
.##..

After 2 minutes:
#####
....#
....#
...#.
#.###

After 3 minutes:
#....
####.
...##
#.##.
.##.#

After 4 minutes:
####.
....#
##..#
.....
##...
To understand the nature of the bugs, watch for the first time a layout of bugs and empty spaces matches any previous layout. In the example above, the first layout to appear twice is:

.....
.....
.....
#....
.#...
To calculate the biodiversity rating for this layout, consider each tile left-to-right in the top row, then left-to-right in the second row, and so on. Each of these tiles is worth biodiversity points equal to increasing powers of two: 1, 2, 4, 8, 16, 32, and so on. Add up the biodiversity points for tiles with bugs; in this example, the 16th tile (32768 points) and 22nd tile (2097152 points) have bugs, a total biodiversity rating of 2129920.

What is the biodiversity rating for the first layout that appears twice?
	*/
	/*
S FN="c:\inetpub\wwwroot\nikuv\image\wbemployee\YUVGZ19P22.txt"
S FNE1="c:\inetpub\wwwroot\nikuv\image\wbemployee\YUVGZ19P22E1.txt"
S FNE2="c:\inetpub\wwwroot\nikuv\image\wbemployee\YUVGZ19P22E2.txt"
S FNE3="c:\inetpub\wwwroot\nikuv\image\wbemployee\YUVGZ19P22E3.txt"
S FNE4="c:\inetpub\wwwroot\nikuv\image\wbemployee\YUVGZ19P22E4.txt"
W $$Part1^YUVGZ201924(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC	;
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	merge CurrentState=RecordsArr
	set BiodiversityRating=0
	for i=1:1 {
		//w !,i,!
		//h .1
		//zw CurrentState
		merge AllStates($increment(AllStates))=CurrentState
		do GetNextState(.CurrentState)
		set BiodiversityRating=$$CheckIfExists(.CurrentState,.AllStates)
		quit:BiodiversityRating
	}
	write !!!
	zwrite CurrentState
	write !!!
	quit BiodiversityRating
}
GetNextState(CurrentState)
{
	set Line="" for  {
		set Line=$order(CurrentState(Line)) quit:Line=""
		set Data=CurrentState(Line)
		for i=1:1:$length(Data) {
			set Char=$extract(Data,i),NextChar=Char
			set NumOfBugs=0
			if $extract(Data,i-1)="#" set NumOfBugs=NumOfBugs+1
			if $extract(Data,i+1)="#" set NumOfBugs=NumOfBugs+1
			if $extract($get(CurrentState(Line+1)),i)="#" set NumOfBugs=NumOfBugs+1
			if $extract($get(CurrentState(Line-1)),i)="#" set NumOfBugs=NumOfBugs+1
			if Char="#",NumOfBugs'=1 set NextChar="."
			if Char=".",(NumOfBugs=1)||(NumOfBugs=2) set NextChar="#"
			set $extract(NextState(Line),i)=NextChar
		}
	}
	merge CurrentState=NextState
}
CheckIfExists(CurrentState,AllStates)
{
	set State="" for  {
		set State=$order(AllStates(State)) quit:State=""
		set SW=1
		set Line="" for  {
			set Line=$order(CurrentState(Line)) quit:Line=""
			if AllStates(State,Line)'=CurrentState(Line) {
				set SW=0
				quit
			}
		}
		quit:SW
	}
	quit:'State 0
	set BiodiversityRating=0
	set Line="" for  {
		set Line=$order(CurrentState(Line)) quit:Line=""
		set Data=CurrentState(Line)
		for i=1:1:$length(Data) {
			continue:$extract(Data,i)'="#"
			set Power=(Line-1)*5+i-1
			set BiodiversityRating=BiodiversityRating+(2**Power)
		}
	}
	quit BiodiversityRating
}
	/*
After careful analysis, one thing is certain: you have no idea where all these bugs are coming from.

Then, you remember: Eris is an old Plutonian settlement! Clearly, the bugs are coming from recursively-folded space.

This 5x5 grid is only one level in an infinite number of recursion levels. The tile in the middle of the grid is actually another 5x5 grid, the grid in your scan is contained as the middle tile of a larger 5x5 grid, and so on. Two levels of grids look like this:

     |     |         |     |     
     |     |         |     |     
     |     |         |     |     
-----+-----+---------+-----+-----
     |     |         |     |     
     |     |         |     |     
     |     |         |     |     
-----+-----+---------+-----+-----
     |     | | | | | |     |     
     |     |-+-+-+-+-|     |     
     |     | | | | | |     |     
     |     |-+-+-+-+-|     |     
     |     | | |?| | |     |     
     |     |-+-+-+-+-|     |     
     |     | | | | | |     |     
     |     |-+-+-+-+-|     |     
     |     | | | | | |     |     
-----+-----+---------+-----+-----
     |     |         |     |     
     |     |         |     |     
     |     |         |     |     
-----+-----+---------+-----+-----
     |     |         |     |     
     |     |         |     |     
     |     |         |     |     
(To save space, some of the tiles are not drawn to scale.) Remember, this is only a small part of the infinitely recursive grid; there is a 5x5 grid that contains this diagram, and a 5x5 grid that contains that one, and so on. Also, the ? in the diagram contains another 5x5 grid, which itself contains another 5x5 grid, and so on.

The scan you took (your puzzle input) shows where the bugs are on a single level of this structure. The middle tile of your scan is empty to accommodate the recursive grids within it. Initially, no other levels contain bugs.

Tiles still count as adjacent if they are directly up, down, left, or right of a given tile. Some tiles have adjacent tiles at a recursion level above or below its own level. For example:

     |     |         |     |     
  1  |  2  |    3    |  4  |  5  
     |     |         |     |     
-----+-----+---------+-----+-----
     |     |         |     |     
  6  |  7  |    8    |  9  |  10 
     |     |         |     |     
-----+-----+---------+-----+-----
     |     |A|B|C|D|E|     |     
     |     |-+-+-+-+-|     |     
     |     |F|G|H|I|J|     |     
     |     |-+-+-+-+-|     |     
 11  | 12  |K|L|?|N|O|  14 |  15 
     |     |-+-+-+-+-|     |     
     |     |P|Q|R|S|T|     |     
     |     |-+-+-+-+-|     |     
     |     |U|V|W|X|Y|     |     
-----+-----+---------+-----+-----
     |     |         |     |     
 16  | 17  |    18   |  19 |  20 
     |     |         |     |     
-----+-----+---------+-----+-----
     |     |         |     |     
 21  | 22  |    23   |  24 |  25 
     |     |         |     |     
Tile 19 has four adjacent tiles: 14, 18, 20, and 24.
Tile G has four adjacent tiles: B, F, H, and L.
Tile D has four adjacent tiles: 8, C, E, and I.
Tile E has four adjacent tiles: 8, D, 14, and J.
Tile 14 has eight adjacent tiles: 9, E, J, O, T, Y, 15, and 19.
Tile N has eight adjacent tiles: I, O, S, and five tiles within the sub-grid marked ?.
The rules about bugs living and dying are the same as before.

For example, consider the same initial state as above:

....#
#..#.
#.?##
..#..
#....
The center tile is drawn as ? to indicate the next recursive grid. Call this level 0; the grid within this one is level 1, and the grid that contains this one is level -1. Then, after ten minutes, the grid at each level would look like this:

Depth -5:
..#..
.#.#.
..?.#
.#.#.
..#..

Depth -4:
...#.
...##
..?..
...##
...#.

Depth -3:
#.#..
.#...
..?..
.#...
#.#..

Depth -2:
.#.##
....#
..?.#
...##
.###.

Depth -1:
#..##
...##
..?..
...#.
.####

Depth 0:
.#...
.#.##
.#?..
.....
.....

Depth 1:
.##..
#..##
..?.#
##.##
#####

Depth 2:
###..
##.#.
#.?..
.#.##
#.#..

Depth 3:
..###
.....
#.?..
#....
#...#

Depth 4:
.###.
#..#.
#.?..
##.#.
.....

Depth 5:
####.
#..#.
#.?#.
####.
.....
In this example, after 10 minutes, a total of 99 bugs are present.

Starting with your scan, how many bugs are present after 200 minutes?
	*/
Part2(FileName="",RecordsArr) PUBLIC	;
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set LastX=$length(RecordsArr(1)),LastY=$order(RecordsArr(""),-1)
	set MiddleX=LastX\2+1,MiddleY=LastY\2+1
	merge GridLevels(0)=RecordsArr
	set $extract(GridLevels(0,MiddleY),MiddleX)="."
	for i=1:1:LastY {
		for j=1:1:LastX {
			set $extract(EmptyGrid(i),j)="."
		}
	}
	set TimesToRun=200
	;set TimesToRun=10
	for i=1:1:TimesToRun {
		//w !!!!,i,!
		set TopLevel=$order(GridLevels(""))
		set BottomLevel=$order(GridLevels(""),-1)
		merge GridLevels(TopLevel-1)=EmptyGrid
		merge GridLevels(BottomLevel+1)=EmptyGrid
		//zw GridLevels
		set Level="" for  {
			set Level=$order(GridLevels(Level)) quit:Level=""
			merge Grid=GridLevels(Level)
			do GetNextState2(.Grid,.GridLevels,Level,MiddleX,MiddleY,LastX,LastY)
			merge NextGridLevels(Level)=Grid
		}
		merge GridLevels=NextGridLevels
	}
	set TotalBugs=0
	set Level="" for  {
		set Level=$order(GridLevels(Level)) quit:Level=""
		set Line="" for  {
			set Line=$order(GridLevels(Level,Line)) quit:Line=""
			set Data=GridLevels(Level,Line)
			for i=1:1:$length(Data) {
				if $extract(Data,i)="#" set TotalBugs=TotalBugs+1
			}
		}
	}
	//w !!!
	//zw GridLevels
	//w !!!
	quit TotalBugs
}
GetNextState2(CurrentState,GridLevels,Level,MiddleX,MiddleY,LastX,LastY)
{
	set Line="" for  {
		set Line=$order(CurrentState(Line)) quit:Line=""
		set Data=CurrentState(Line)
		for i=1:1:$length(Data) {
			set Char=$extract(Data,i),NextChar=Char
			set NumOfBugs=0
			//Regular check
			if $extract(Data,i-1)="#" set NumOfBugs=NumOfBugs+1
			if $extract(Data,i+1)="#" set NumOfBugs=NumOfBugs+1
			if $extract($get(CurrentState(Line+1)),i)="#" set NumOfBugs=NumOfBugs+1
			if $extract($get(CurrentState(Line-1)),i)="#" set NumOfBugs=NumOfBugs+1
			if $data(GridLevels(Level-1)) {
				//Outer level grid
				if Line=1,$extract(GridLevels(Level-1,MiddleY-1),MiddleX)="#" set NumOfBugs=NumOfBugs+1
				if Line=LastY,$extract(GridLevels(Level-1,MiddleY+1),MiddleX)="#" set NumOfBugs=NumOfBugs+1
				if i=1,$extract(GridLevels(Level-1,MiddleY),MiddleX-1)="#" set NumOfBugs=NumOfBugs+1
				if i=LastX,$extract(GridLevels(Level-1,MiddleY),MiddleX+1)="#" set NumOfBugs=NumOfBugs+1
			}
			if $data(GridLevels(Level+1)) {
				//Inner level grid
				if i=MiddleX,Line+1=MiddleY {
					//Get all bugs from first row in inner grid
					for j=1:1:LastX {
						if $extract(GridLevels(Level+1,1),j)="#" set NumOfBugs=NumOfBugs+1
					}
				}
				if i=MiddleX,Line-1=MiddleY {
					//Get all bugs from last row in inner grid
					for j=1:1:LastX {
						if $extract(GridLevels(Level+1,LastY),j)="#" set NumOfBugs=NumOfBugs+1
					}
				}
				if i+1=MiddleX,Line=MiddleY {
					//Get all bugs from first column in inner grid
					for j=1:1:LastY {
						if $extract(GridLevels(Level+1,j),1)="#" set NumOfBugs=NumOfBugs+1
					}
				}
				if i-1=MiddleX,Line=MiddleY {
					//Get all bugs from last column in inner grid
					for j=1:1:LastY {
						if $extract(GridLevels(Level+1,j),LastX)="#" set NumOfBugs=NumOfBugs+1
					}
				}
			}
			if Char="#",NumOfBugs'=1 set NextChar="."
			if Char=".",(NumOfBugs=1)||(NumOfBugs=2) set NextChar="#"
			set $extract(NextState(Line),i)=NextChar
		}
	}
	merge CurrentState=NextState
	set $extract(CurrentState(MiddleY),MiddleX)="."
}

CheckData(RecordsArr) {
	set ERR=""
	set NumOfLines=$order(RecordsArr(""),-1)
	if '(NumOfLines#2) {
		write !,"Wrong number of lines ("_NumOfLines_")"
		quit 0
	}
	set LineLength=$length($get(RecordsArr(1)))
	if 'LineLength {
		write !,"Missing data in line 1"
		quit 0
	}
	if '(LineLength#2) {
		write !,"Wrong number of tiles ("_LineLength_")"
		quit 0
	}

	set Line="" for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		if $data(RecordsArr(Line))'=1 {
			set ERR="Wrong data"
			write !,ERR
			quit
		}
		set Data=RecordsArr(Line)
		set DefaultError="Wrong data in line "_Line_"("_Data_")"
		if LineLength'=$length(Data) {
			set ERR=$$DisplayErr(DefaultError_"-1") quit
		}
		if $length($translate(Data,"#.")) {
			set ERR=$$DisplayErr(DefaultError_"-2") quit
		}
	}
	quit:$length(ERR) 0
	quit 1
}
DisplayErr(Error) {
	write !,Error
	quit Error
}
LoadFile(FileName,RecordsArr)	;
{
	kill RecordsArr
	open FileName::2 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		if $length(Record) set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
