YUVGZ20194	;https://adventofcode.com/2019/day/4;
	quit
	/*
Your puzzle input is 134564-585159
	*/
	/*
You arrive at the Venus fuel depot only to discover it's protected by a password. The Elves had written the password on a sticky note, but someone threw it out.

However, they do remember a few key facts about the password:

It is a six-digit number.
The value is within the range given in your puzzle input.
Two adjacent digits are the same (like 22 in 122345).
Going from left to right, the digits never decrease; they only ever increase or stay the same (like 111123 or 135679).
Other than the range rule, the following are true:

111111 meets these criteria (double 11, never decreases).
223450 does not meet these criteria (decreasing pair of digits 50).
123789 does not meet these criteria (no double).
How many different passwords within the range given in your puzzle input meet these criteria?
	*/
Part1(PasswordRange) PUBLIC	;
{
	/*set Filename=$get(Filename)
	if $data(ModuleData)<10 {
		if '$length(Filename) {
			write !,"Enter file location: " read Filename
		}
		if '$length(Filename) {
			write !,"No data enterd, so bye bye for now"
			quit 0
		}
		if '$$LoadFile(Filename,.ModuleData) {
			write !,"Can't open file "_Filename_" for reading, bye bye"
			quit 0
		}
	}
	if '$$IsDataOk(.ModuleData) quit 0
	*/
	if '$length($get(PasswordRange)) {
		read !,"Please enter the range to search for valid password: ",PasswordRange
		write !
	}
	if PasswordRange'?6N1"-"6N {
		write !,PasswordRange_" is not a valid range"
		quit 0
	}
	set From=$piece(PasswordRange,"-")
	set To=$piece(PasswordRange,"-",2)
	if (+From'=From)||(+To'=To) {
		write !,PasswordRange_" is not a valid range"
		quit 0
	}
	if From>To {
		write !,From_" is bigger than "_To,!,PasswordRange_" is not a valid range"
		quit 0
	}
	set MaybePasswordOK=0
	for Password=From:1:To {
		set AdjacentDigitsSW=0,DecreaseSW=0
		for DigitLoc=1:1:5 {
			if $extract(Password,DigitLoc)=$extract(Password,DigitLoc+1) set AdjacentDigitsSW=1
			if $extract(Password,DigitLoc)>$extract(Password,DigitLoc+1) {
				set DecreaseSW=1
				quit
			}
		}
		continue:'AdjacentDigitsSW
		continue:DecreaseSW
		
		set MaybePasswordOK=MaybePasswordOK+1
	}
	quit MaybePasswordOK
}

	/*
An Elf just remembered one more important detail: the two adjacent matching digits are not part of a larger group of matching digits.

Given this additional criterion, but still ignoring the range rule, the following are now true:

112233 meets these criteria because the digits never decrease and all repeated digits are exactly two digits long.
123444 no longer meets the criteria (the repeated 44 is part of a larger group of 444).
111122 meets the criteria (even though 1 is repeated more than twice, it still contains a double 22).
How many different passwords within the range given in your puzzle input meet all of the criteria?
	*/
Part2(PasswordRange) PUBLIC	;
{
	/*set Filename=$get(Filename)
	if $data(ModuleData)<10 {
		if '$length(Filename) {
			write !,"Enter file location: " read Filename
		}
		if '$length(Filename) {
			write !,"No data enterd, so bye bye for now"
			quit 0
		}
		if '$$LoadFile(Filename,.ModuleData) {
			write !,"Can't open file "_Filename_" for reading, bye bye"
			quit 0
		}
	}
	if '$$IsDataOk(.ModuleData) quit 0
	*/
	if '$length($get(PasswordRange)) {
		read !,"Please enter the range to search for valid password: ",PasswordRange
		write !
	}
	if PasswordRange'?6N1"-"6N {
		write !,PasswordRange_" is not a valid range"
		quit 0
	}
	set From=$piece(PasswordRange,"-")
	set To=$piece(PasswordRange,"-",2)
	if (+From'=From)||(+To'=To) {
		write !,PasswordRange_" is not a valid range"
		quit 0
	}
	if From>To {
		write !,From_" is bigger than "_To,!,PasswordRange_" is not a valid range"
		quit 0
	}
	set MaybePasswordOK=0
	for Password=From:1:To {
		set AdjacentDigitsSW=0,DecreaseSW=0
		for DigitLoc=1:1:5 {
			set Digit=$extract(Password,DigitLoc)
			if Digit>$extract(Password,DigitLoc+1) {
				set DecreaseSW=1
				quit
			}
		}
		continue:DecreaseSW
		for DigitLoc=1:1:5 {
			set Digit=$extract(Password,DigitLoc)
			set Temp=Digit
			for InnerDigitLoc=DigitLoc+1:1:6 {
				set TempDigit=$extract(Password,InnerDigitLoc)
				if TempDigit'=Digit quit
				set Temp=Temp_Digit
				set DigitLoc=DigitLoc+1
			}
			if Temp>10,Temp<100 set AdjacentDigitsSW=1
		}
		continue:'AdjacentDigitsSW
		
		set MaybePasswordOK=MaybePasswordOK+1
		//w Password,! h 1
	}
	quit MaybePasswordOK
}

LoadFile(Filename,LineArr)	;
{
	kill LineArr
	set eofSW=$zutil(68,40,1)
	open Filename::2 quit:'$test 0
	use Filename for Counter=1:1 read Line quit:$zeof  set:$length(Line) LineArr(Counter)=Line
	close Filename
	if 'eofSW do $zutil(68,40,0)
	quit 1
}
