ROUTINE YUVGZ201811 [Type=INT]
YUVGZ201811	;https://adventofcode.com/2018/day/11;
	//Intersystems developer community id: 130669-ab1f69bf
	//
	//
	quit
	/*
Input data:
4151
	*/
	/*
Year 2018, Day 11 part 1:
--- Day 11: Chronal Charge ---
You watch the Elves and their sleigh fade into the distance as they head toward the North Pole.

Actually, you're the one fading. The falling sensation returns.

The low fuel warning light is illuminated on your wrist-mounted device. Tapping it once causes it to project a hologram of the situation: a 300x300 grid of fuel cells and their current power levels, some negative. You're not sure what negative power means in the context of time travel, but it can't be good.

Each fuel cell has a coordinate ranging from 1 to 300 in both the X (horizontal) and Y (vertical) direction. In X,Y notation, the top-left cell is 1,1, and the top-right cell is 300,1.

The interface lets you select any 3x3 square of fuel cells. To increase your chances of getting to your destination, you decide to choose the 3x3 square with the largest total power.

The power level in a given fuel cell can be found through the following process:

Find the fuel cell's rack ID, which is its X coordinate plus 10.
Begin with a power level of the rack ID times the Y coordinate.
Increase the power level by the value of the grid serial number (your puzzle input).
Set the power level to itself multiplied by the rack ID.
Keep only the hundreds digit of the power level (so 12345 becomes 3; numbers with no hundreds digit become 0).
Subtract 5 from the power level.
For example, to find the power level of the fuel cell at 3,5 in a grid with serial number 8:

The rack ID is 3 + 10 = 13.
The power level starts at 13 * 5 = 65.
Adding the serial number produces 65 + 8 = 73.
Multiplying by the rack ID produces 73 * 13 = 949.
The hundreds digit of 949 is 9.
Subtracting 5 produces 9 - 5 = 4.
So, the power level of this fuel cell is 4.

Here are some more example power levels:

Fuel cell at  122,79, grid serial number 57: power level -5.
Fuel cell at 217,196, grid serial number 39: power level  0.
Fuel cell at 101,153, grid serial number 71: power level  4.
Your goal is to find the 3x3 square which has the largest total power. The square must be entirely within the 300x300 grid. Identify this square using the X,Y coordinate of its top-left fuel cell. For example:

For grid serial number 18, the largest total 3x3 square has a top-left corner of 33,45 (with a total power of 29); these fuel cells appear in the middle of this 5x5 region:

-2  -4   4   4   4
-4   4   4   4  -5
 4   3   3   4  -4
 1   1   2   4  -3
-1   0   2  -5  -2
For grid serial number 42, the largest 3x3 square's top-left is 21,61 (with a total power of 30); they are in the middle of this region:

-3   4   2   2   2
-4   4   3   3   4
-5   3   3   4  -4
 4   3   3   4  -3
 3   3   3  -5  -1
What is the X,Y coordinate of the top-left fuel cell of the 3x3 square with the largest total power?
	*/
	/*
W $$Part1^YUVGZ201811(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Serial=RecordsArr(1)
	for X=1:1:300 {
		set RackId=X+10
		for Y=1:1:300 {
			set PowerLevel=(RackId*Y+Serial)*RackId
			set Digit=PowerLevel#1000\100-5
			set Grid(X,Y)=Digit
		}
	}
	//write !,Grid(3,5),!,Grid(122,79),!,Grid(217,196),!,Grid(101,153),!
	set Max=-999999999999999,MaxX=0,MaxY=0
	for X=1:1:298 {
		for Y=1:1:298 {
			set Tot=$$GetTotal3X3(.Grid,X,Y)
			set:Tot>Max Max=Tot,MaxX=X,MaxY=Y
		}
	}
	set ANS=$listbuild(MaxX,MaxY)
	//
	//
	//
	quit ANS
}
GetTotal3X3(&Grid,FromX,FromY)
{
	quit $$GetTotalSize(.Grid,FromX,FromY,3)
}
GetTotalSize(&Grid,FromX,FromY,Size)
{
	set Total=0
	for X=FromX:1:FromX+Size-1 {
		for Y=FromY:1:FromY+Size-1 {
			set Total=Total+Grid(X,Y)
		}
	}
	quit Total
}
JobGetTotalSize(Size) PUBLIC
{
	merge Grid=^Grid
	set Max=-999999999999999,(MaxX,MaxY)=0
	for X=1:1:300-Size+1 {
		for Y=1:1:300-Size+1 {
			set Tot=$$GetTotalSize(.Grid,X,Y,Size)
			set:Tot>Max Max=Tot,MaxX=X,MaxY=Y
		}
	}
	write !,Max,",",MaxX,",",MaxY
	set ^GridSize(Size)=$listbuild(Max,MaxX,MaxY)
}
	/*
Year 2018, Day 11 part 2:
You discover a dial on the side of the device; it seems to let you select a square of any size, not just 3x3. Sizes from 1x1 to 300x300 are supported.

Realizing this, you now must find the square of any size with the largest total power. Identify this square by including its size as a third parameter after the top-left coordinate: a 9x9 square with a top-left corner of 3,5 is identified as 3,5,9.

For example:

For grid serial number 18, the largest total square (with a total power of 113) is 16x16 and has a top-left corner of 90,269, so its identifier is 90,269,16.
For grid serial number 42, the largest total square (with a total power of 119) is 12x12 and has a top-left corner of 232,251, so its identifier is 232,251,12.
What is the X,Y,size identifier of the square with the largest total power?
	*/
	/*
W $$Part2^YUVGZ201811(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Serial=RecordsArr(1)
	for X=1:1:300 {
		set RackId=X+10
		for Y=1:1:300 {
			set PowerLevel=(RackId*Y+Serial)*RackId
			set Digit=PowerLevel#1000\100-5
			set Grid(X,Y)=Digit
		}
	}
	K ^Grid,^GridSize
	m ^Grid=Grid
	//write !,Grid(3,5),!,Grid(122,79),!,Grid(217,196),!,Grid(101,153),!
	set Max=-999999999999999,(MaxX,MaxY,MaxSize)=0
	for Size=1:1:300 {
		write !,Size
		job JobGetTotalSize(Size)
	}
	for Size=1:1:300 {
		for  quit:$data(^GridSize(Size))  hang .000001
		set Data=^GridSize(Size)
		set Total=$list(Data,1)
		if Total>Max {
			set Max=Total,MaxX=$list(Data,2),MaxY=$list(Data,3),MaxSize=Size
		}
	}
	K ^Grid,^GridSize
	set ANS=$listbuild(MaxX,MaxY,MaxSize)
	//
	//
	//
	quit ANS
}
CheckData(RecordsArr)
{
	if ($order(RecordsArr(""))'=1)||($order(RecordsArr(""),-1)'=1) {
		set Error="Only one line of data allowed"
		write !,Error
		quit 0
	}
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		if Record'?1.N {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
