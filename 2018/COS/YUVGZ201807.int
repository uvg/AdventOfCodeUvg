ROUTINE YUVGZ201807 [Type=INT]
YUVGZ201807	;https://adventofcode.com/2018/day/7;
	//Intersystems developer community id: 130669-ab1f69bf
	//
	//Day 7: The Sum of Its Parts
	quit
	/*
Input data:
Step W must be finished before step G can begin.
Step N must be finished before step X can begin.
Step M must be finished before step O can begin.
Step S must be finished before step I can begin.
Step F must be finished before step Y can begin.
Step Q must be finished before step K can begin.
Step K must be finished before step Y can begin.
Step Z must be finished before step J can begin.
Step G must be finished before step L can begin.
Step J must be finished before step C can begin.
Step R must be finished before step E can begin.
Step X must be finished before step I can begin.
Step P must be finished before step E can begin.
Step V must be finished before step Y can begin.
Step C must be finished before step I can begin.
Step O must be finished before step H can begin.
Step T must be finished before step B can begin.
Step Y must be finished before step A can begin.
Step E must be finished before step L can begin.
Step B must be finished before step D can begin.
Step L must be finished before step U can begin.
Step A must be finished before step I can begin.
Step I must be finished before step D can begin.
Step H must be finished before step D can begin.
Step U must be finished before step D can begin.
Step B must be finished before step I can begin.
Step S must be finished before step F can begin.
Step M must be finished before step R can begin.
Step A must be finished before step H can begin.
Step Z must be finished before step O can begin.
Step K must be finished before step I can begin.
Step K must be finished before step D can begin.
Step B must be finished before step A can begin.
Step G must be finished before step I can begin.
Step Z must be finished before step B can begin.
Step R must be finished before step P can begin.
Step J must be finished before step E can begin.
Step R must be finished before step I can begin.
Step Q must be finished before step U can begin.
Step S must be finished before step Z can begin.
Step E must be finished before step I can begin.
Step F must be finished before step E can begin.
Step F must be finished before step I can begin.
Step S must be finished before step J can begin.
Step O must be finished before step I can begin.
Step V must be finished before step B can begin.
Step A must be finished before step U can begin.
Step M must be finished before step T can begin.
Step K must be finished before step A can begin.
Step L must be finished before step I can begin.
Step I must be finished before step U can begin.
Step G must be finished before step U can begin.
Step B must be finished before step U can begin.
Step E must be finished before step D can begin.
Step J must be finished before step T can begin.
Step M must be finished before step Y can begin.
Step P must be finished before step B can begin.
Step M must be finished before step S can begin.
Step E must be finished before step U can begin.
Step R must be finished before step Y can begin.
Step J must be finished before step I can begin.
Step J must be finished before step D can begin.
Step Y must be finished before step E can begin.
Step A must be finished before step D can begin.
Step X must be finished before step H can begin.
Step O must be finished before step E can begin.
Step E must be finished before step B can begin.
Step E must be finished before step A can begin.
Step F must be finished before step U can begin.
Step G must be finished before step J can begin.
Step M must be finished before step Z can begin.
Step Y must be finished before step U can begin.
Step Y must be finished before step D can begin.
Step S must be finished before step D can begin.
Step G must be finished before step H can begin.
Step C must be finished before step Y can begin.
Step B must be finished before step H can begin.
Step P must be finished before step V can begin.
Step M must be finished before step K can begin.
Step L must be finished before step A can begin.
Step G must be finished before step A can begin.
Step Q must be finished before step P can begin.
Step P must be finished before step I can begin.
Step H must be finished before step U can begin.
Step G must be finished before step X can begin.
Step L must be finished before step H can begin.
Step X must be finished before step P can begin.
Step Z must be finished before step Y can begin.
Step N must be finished before step K can begin.
Step Q must be finished before step X can begin.
Step X must be finished before step L can begin.
Step T must be finished before step Y can begin.
Step P must be finished before step A can begin.
Step C must be finished before step T can begin.
Step J must be finished before step V can begin.
Step X must be finished before step O can begin.
Step S must be finished before step C can begin.
Step R must be finished before step C can begin.
Step E must be finished before step H can begin.
Step V must be finished before step H can begin.
Step L must be finished before step D can begin.
	*/
	/*
Year 2018, Day 7 part 1:
You find yourself standing on a snow-covered coastline; apparently, you landed a little off course. The region is too hilly to see the North Pole from here, but you do spot some Elves that seem to be trying to unpack something that washed ashore. It's quite cold out, so you decide to risk creating a paradox by asking them for directions.

"Oh, are you the search party?" Somehow, you can understand whatever Elves from the year 1018 speak; you assume it's Ancient Nordic Elvish. Could the device on your wrist also be a translator? "Those clothes don't look very warm; take this." They hand you a heavy coat.

"We do need to find our way back to the North Pole, but we have higher priorities at the moment. You see, believe it or not, this box contains something that will solve all of Santa's transportation problems - at least, that's what it looks like from the pictures in the instructions." It doesn't seem like they can read whatever language it's in, but you can: "Sleigh kit. Some assembly required."

"'Sleigh'? What a wonderful name! You must help us assemble this 'sleigh' at once!" They start excitedly pulling more parts out of the box.

The instructions specify a series of steps and requirements about which steps must be finished before others can begin (your puzzle input). Each step is designated by a single letter. For example, suppose you have the following instructions:

Step C must be finished before step A can begin.
Step C must be finished before step F can begin.
Step A must be finished before step B can begin.
Step A must be finished before step D can begin.
Step B must be finished before step E can begin.
Step D must be finished before step E can begin.
Step F must be finished before step E can begin.
Visually, these requirements look like this:

  -->A--->B--
 /    \      \
C      -->D----->E
 \           /
  ---->F-----
Your first goal is to determine the order in which the steps should be completed. If more than one step is ready, choose the step which is first alphabetically. In this example, the steps would be completed as follows:

Only C is available, and so it is done first.
Next, both A and F are available. A is first alphabetically, so it is done next.
Then, even though F was available earlier, steps B and D are now also available, and B is the first alphabetically of the three.
After that, only D and F are available. E is not available because only some of its prerequisites are complete. Therefore, D is completed next.
F is the only choice, so it is done next.
Finally, E is completed.
So, in this example, the correct order is CABDFE.

In what order should the steps in your instructions be completed?
	*/
	/*
W $$Part1^YUVGZ201807(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	do AnalayzeData(.RecordsArr,.Father2Son,.Son2Father,.All)
	//zwrite RecordsArr,Father2Son,Son2Father
	set String=""
	for  {
		quit:'$data(Son2Father)
		set Father=""
		for  {
			set Father=$order(Father2Son(Father)) quit:Father=""
			continue:$data(Son2Father(Father))
			set String=String_Father
			set Son=""
			for  {
				set Son=$order(Father2Son(Father,Son)) quit:Son=""
				kill Son2Father(Son,Father)
			}
			kill Father2Son(Father),All(Father)
			quit
		}
	}
	set Son=""
	for  {
		set Son=$order(All(Son)) quit:Son=""
		set String=String_Son
	}
	set ANS=String
	//
	//
	//
	quit ANS
}
AnalayzeData(&RecordsArr,&Father2Son,&Son2Father,&All)
{
	set Line=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		set Father=$piece(Record," ",2),Son=$piece(Record," ",8)
		set Father2Son(Father,Son)=""
		set Son2Father(Son,Father)=""
		set (All(Son),All(Father))=""
	}
}

	/*
Year 2018, Day 7 part 2:
As you're about to begin construction, four of the Elves offer to help. "The sun will set soon; it'll go faster if we work together." Now, you need to account for multiple people working on steps simultaneously. If multiple steps are available, workers should still begin them in alphabetical order.

Each step takes 60 seconds plus an amount corresponding to its letter: A=1, B=2, C=3, and so on. So, step A takes 60+1=61 seconds, while step Z takes 60+26=86 seconds. No time is required between steps.

To simplify things for the example, however, suppose you only have help from one Elf (a total of two workers) and that each step takes 60 fewer seconds (so that step A takes 1 second and step Z takes 26 seconds). Then, using the same instructions as above, this is how each second would be spent:

Second   Worker 1   Worker 2   Done
   0        C          .        
   1        C          .        
   2        C          .        
   3        A          F       C
   4        B          F       CA
   5        B          F       CA
   6        D          F       CAB
   7        D          F       CAB
   8        D          F       CAB
   9        D          .       CABF
  10        E          .       CABFD
  11        E          .       CABFD
  12        E          .       CABFD
  13        E          .       CABFD
  14        E          .       CABFD
  15        .          .       CABFDE
Each row represents one second of time. The Second column identifies how many seconds have passed as of the beginning of that second. Each worker column shows the step that worker is currently doing (or . if they are idle). The Done column shows completed steps.

Note that the order of the steps has changed; this is because steps now take time to finish and multiple workers can begin multiple steps simultaneously.

In this example, it would take 15 seconds for two workers to complete these steps.

With 5 workers and the 60+ second step durations described above, how long will it take to complete all of the steps?
	*/
	/*
W $$Part2^YUVGZ201807(FN)
	*/
Part2(FileName="",RecordsArr,Offset,NumOfWorker) PUBLIC
{
	if $get(Offset)'?1.N||'$extract(Offset) set Offset=0
	if '$length($get(NumOfWorker)) {
		read !,"Enter number of workers: ",NumOfWorker
		write !
	}
	if $get(NumOfWorker)'?1.N||'$extract(NumOfWorker) {
		write !,"Wrong number of workers"
		quit 0
	}
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	do AnalayzeData(.RecordsArr,.Father2Son,.Son2Father,.All)
	//zwrite RecordsArr,Father2Son,Son2Father
	set String="",Start=0
	for I=1:1:NumOfWorker set Worker(I)=""
	for  {
		quit:'$data(Son2Father)
		//
		//Load workers with some work
		set Father=""
		for  {
			set Father=$order(Father2Son(Father)) quit:Father=""
			continue:$data(Son2Father(Father))
			continue:$data(AtWork(Father))
			for I=1:1:NumOfWorker {
				continue:$length(Worker(I))
				//Find a worker and set him work
				set Worker(I)=$listbuild(Start,Father,$ascii(Father)-64+Offset)
				set AtWork(Father)=""
				quit
			}
		}
		for I=1:1:NumOfWorker if $length(Worker(I)) set $list(Worker(I),4)=$listget(Worker(I),4)+1 //Increment work
		set Start=Start+1
		//w !!!,"Before:",!
		//zw Worker,AtWork,String
		for I=1:1:NumOfWorker {
			set Data=Worker(I)
			continue:'$length(Data)
			continue:$list(Data,3)'=$list(Data,4)
			//Find workers that finished their work
			//
			set Father=$list(Data,2)
			set String=String_Father,Worker(I)=""
			set Son=""
			for  {
				set Son=$order(Father2Son(Father,Son)) quit:Son=""
				kill Son2Father(Son,Father)
			}
			kill Father2Son(Father),All(Father),AtWork(Father)
		}
		//w !,"After:",!
		//zw Worker,AtWork,String
	}
	//ZW All
	set Son=""
	for  {
		set Son=$order(All(Son)) quit:Son=""
		set String=String_Son,Start=Start+$ascii(Son)-64+Offset
	}
	set ANS=Start //_"*"_String
	//
	//
	//
	quit ANS
}
CheckData(RecordsArr)
{
	//Step W must be finished before step G can begin.
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		if Record'?1"Step "1U1" must be finished before step "1U1" can begin." {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
