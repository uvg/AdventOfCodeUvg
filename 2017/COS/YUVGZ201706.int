ROUTINE YUVGZ201706 [Type=INT]
YUVGZ201706	;https://adventofcode.com/2017/day/6;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
Input data:
14	0	15	12	11	11	3	5	1	6	8	4	9	1	8	4
	*/
	/*
Year 2017, Day 6 part 1:
--- Day 6: Memory Reallocation ---
A debugger program here is having an issue: it is trying to repair a memory reallocation routine, but it keeps getting stuck in an infinite loop.

In this area, there are sixteen memory banks; each memory bank can hold any number of blocks. The goal of the reallocation routine is to balance the blocks between the memory banks.

The reallocation routine operates in cycles. In each cycle, it finds the memory bank with the most blocks (ties won by the lowest-numbered memory bank) and redistributes those blocks among the banks. To do this, it removes all of the blocks from the selected bank, then moves to the next (by index) memory bank and inserts one of the blocks. It continues doing this until it runs out of blocks; if it reaches the last memory bank, it wraps around to the first one.

The debugger would like to know how many redistributions can be done before a blocks-in-banks configuration is produced that has been seen before.

For example, imagine a scenario with only four memory banks:

The banks start with 0, 2, 7, and 0 blocks. The third bank has the most blocks, so it is chosen for redistribution.
Starting with the next bank (the fourth bank) and then continuing to the first bank, the second bank, and so on, the 7 blocks are spread out over the memory banks. The fourth, first, and second banks get two blocks each, and the third bank gets one back. The final result looks like this: 2 4 1 2.
Next, the second bank is chosen because it contains the most blocks (four). Because there are four memory banks, each gets one block. The result is: 3 1 2 3.
Now, there is a tie between the first and fourth memory banks, both of which have three blocks. The first bank wins the tie, and its three blocks are distributed evenly over the other three banks, leaving it with none: 0 2 3 4.
The fourth bank is chosen, and its four blocks are distributed such that each of the four banks receives one: 1 3 4 1.
The third bank is chosen, and the same thing happens: 2 4 1 2.
At this point, we've reached a state we've seen before: 2 4 1 2 was already seen. The infinite loop is detected after the fifth block redistribution cycle, and so the answer in this example is 5.

Given the initial block counts in your puzzle input, how many redistribution cycles must be completed before a configuration is produced that has been seen before?
	*/
	/*
W $$Part1^YUVGZ201706(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Data=RecordsArr(1)
	for i=1:1:$length(Data,$char(9)) {
		set Memory(i)=+$piece(Data,$char(9),i)
	}
	zwrite Memory
	set steps=0
	for  {
		set data="",i="",maxmem=-1,maxi=0
		for  {
			set i=$order(Memory(i)) quit:i=""
			set mem=Memory(i)
			set:mem>maxmem maxmem=mem,maxi=i
			set data=data_$listbuild(mem)
		}
		quit:$data(arr(data))
		set arr(data)="",steps=steps+1
		set j=maxi,Memory(j)=0
		for  set j=$order(Memory(j)) set:'j j=1 set Memory(j)=Memory(j)+1,maxmem=maxmem-1 quit:'maxmem
	}
	zw data
	set ANS=steps
	//
	//
	//
	quit ANS
}
	/*
Year 2017, Day 6 part 2:
Out of curiosity, the debugger would also like to know the size of the loop: starting from a state that has already been seen, how many block redistribution cycles must be performed before that same state is seen again?

In the example above, 2 4 1 2 is seen again after four cycles, and so the answer in that example would be 4.

How many cycles are in the infinite loop that arises from the configuration in your puzzle input?
	*/
	/*
W $$Part2^YUVGZ201706(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Data=RecordsArr(1)
	for i=1:1:$length(Data,$char(9)) {
		set Memory(i)=+$piece(Data,$char(9),i)
	}
	zwrite Memory
	set steps=0
	for  {
		set data="",i="",maxmem=-1,maxi=0
		for  {
			set i=$order(Memory(i)) quit:i=""
			set mem=Memory(i)
			set:mem>maxmem maxmem=mem,maxi=i
			set data=data_$listbuild(mem)
		}
		if $data(arr(data)) set ANS=steps-arr(data) quit
		set arr(data)=steps,steps=steps+1
		set j=maxi,Memory(j)=0
		for  set j=$order(Memory(j)) set:'j j=1 set Memory(j)=Memory(j)+1,maxmem=maxmem-1 quit:'maxmem
	}
	zw data
	//
	//
	//
	quit ANS
}
CheckData(RecordsArr)
{
	if ($order(RecordsArr(""))'=1)||($order(RecordsArr(""),-1)'=1) {
		set Error="Only one line of data allowed"
		write !,Error
		quit 0
	}
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line),Record=$translate(Record,$char(9)," ")
		if Record'?1.(1.N1" ")1.N {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
