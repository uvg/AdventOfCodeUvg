ROUTINE YUVGZ201724 [Type=INT]
YUVGZ201724	;https://adventofcode.com/2017/day/24;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
Input data:
31/13
34/4
49/49
23/37
47/45
32/4
12/35
37/30
41/48
0/47
32/30
12/5
37/31
7/41
10/28
35/4
28/35
20/29
32/20
31/43
48/14
10/11
27/6
9/24
8/28
45/48
8/1
16/19
45/45
0/4
29/33
2/5
33/9
11/7
32/10
44/1
40/32
2/45
16/16
1/18
38/36
34/24
39/44
32/37
26/46
25/33
9/10
0/29
38/8
33/33
49/19
18/20
49/39
18/39
26/13
19/32
	*/
	/*
Year 2017, Day 24 part 1:
--- Day 24: Electromagnetic Moat ---
The CPU itself is a large, black building surrounded by a bottomless pit. Enormous metal tubes extend outward from the side of the building at regular intervals and descend down into the void. There's no way to cross, but you need to get inside.

No way, of course, other than building a bridge out of the magnetic components strewn about nearby.

Each component has two ports, one on each end. The ports come in all different types, and only matching types can be connected. You take an inventory of the components by their port types (your puzzle input). Each port is identified by the number of pins it uses; more pins mean a stronger connection for your bridge. A 3/7 component, for example, has a type-3 port on one side, and a type-7 port on the other.

Your side of the pit is metallic; a perfect surface to connect a magnetic, zero-pin port. Because of this, the first port you use must be of type 0. It doesn't matter what type of port you end with; your goal is just to make the bridge as strong as possible.

The strength of a bridge is the sum of the port types in each component. For example, if your bridge is made of components 0/3, 3/7, and 7/4, your bridge has a strength of 0+3 + 3+7 + 7+4 = 24.

For example, suppose you had the following components:

0/2
2/2
2/3
3/4
3/5
0/1
10/1
9/10
With them, you could make the following valid bridges:

0/1
0/1--10/1
0/1--10/1--9/10
0/2
0/2--2/3
0/2--2/3--3/4
0/2--2/3--3/5
0/2--2/2
0/2--2/2--2/3
0/2--2/2--2/3--3/4
0/2--2/2--2/3--3/5
(Note how, as shown by 10/1, order of ports within a component doesn't matter. However, you may only use each port on a component once.)

Of these bridges, the strongest one is 0/1--10/1--9/10; it has a strength of 0+1 + 1+10 + 10+9 = 31.

What is the strength of the strongest bridge you can make with the components you have available?
	*/
	/*
W $$Part1^YUVGZ201724(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC	;
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Line=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		set Val1=$piece(Record,"/",1),Val2=$piece(Record,"/",2)
		set Arr(Val1,Val2)=""
		set Arr(Val2,Val1)=""
	}
	zw Arr
	set ANS=$$GetMaxBridge(.Arr,0)
	//
	//
	//
	quit ANS
}
GetMaxBridge(&ConnectionsArr,From)
{
	quit:'$data(ConnectionsArr(From)) 0
	set To="",maxSteps=0
	for  {
		set To=$order(ConnectionsArr(From,To)) quit:To=""
		continue:ConnectionsArr(From,To)
		kill tempConnectionArr
		merge tempConnectionArr=ConnectionsArr
		set tempConnectionArr(From,To)=1
		set tempConnectionArr(To,From)=1
		set steps=From+To+$$GetMaxBridge(.tempConnectionArr,To)
		set:steps>maxSteps maxSteps=steps
	}
	quit maxSteps
}
	/*
Year 2017, Day 24 part 2:
The bridge you've built isn't long enough; you can't jump the rest of the way.

In the example above, there are two longest bridges:

0/2--2/2--2/3--3/4
0/2--2/2--2/3--3/5
Of them, the one which uses the 3/5 component is stronger; its strength is 0+2 + 2+2 + 2+3 + 3+5 = 19.

What is the strength of the longest bridge you can make? If you can make multiple bridges of the longest length, pick the strongest one.
	*/
	/*
W $$Part2^YUVGZ201724(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC	;
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Line=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		set Val1=$piece(Record,"/",1),Val2=$piece(Record,"/",2)
		set Arr(Val1,Val2)=""
		set Arr(Val2,Val1)=""
	}
	zw Arr
	set ANS=$list($$GetMaxBridge2(.Arr,0,0),1)
	//
	//
	//
	quit ANS
}
GetMaxBridge2(&ConnectionsArr,From,Length)
{
	quit:'$data(ConnectionsArr(From)) $listbuild(0,Length)
	set To="",maxSteps=0
	for  {
		set To=$order(ConnectionsArr(From,To)) quit:To=""
		continue:ConnectionsArr(From,To)
		kill tempConnectionArr
		merge tempConnectionArr=ConnectionsArr
		set tempConnectionArr(From,To)=1
		set tempConnectionArr(To,From)=1
		set ans=$$GetMaxBridge2(.tempConnectionArr,To,Length+1)
		set steps=From+To+$list(ans,1),length=$list(ans,2)
		set arr(length,steps)=""
	}
	quit:'$data(arr) $listbuild(0,Length)
	set maxLength=$order(arr(""),-1)
	set maxSteps=$order(arr(maxLength,""),-1)
	quit $listbuild(maxSteps,maxLength)
}
CheckData(RecordsArr)
{
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		if Record'?1.N1"/"1.N {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)	;
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
