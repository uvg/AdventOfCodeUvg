ROUTINE YUVGZ201723 [Type=INT]
YUVGZ201723	;https://adventofcode.com/2017/day/23;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
Input data:
set b 99
set c b
jnz a 2
jnz 1 5
mul b 100
sub b -100000
set c b
sub c -17000
set f 1
set d 2
set e 2
set g d
mul g e
sub g b
jnz g 2
set f 0
sub e -1
set g e
sub g b
jnz g -8
sub d -1
set g d
sub g b
jnz g -13
jnz f 2
sub h -1
set g b
sub g c
jnz g 2
jnz 1 3
sub b -17
jnz 1 -23
	*/
	/*
Year 2017, Day 23 part 1:
--- Day 23: Coprocessor Conflagration ---
You decide to head directly to the CPU and fix the printer from there. As you get close, you find an experimental coprocessor doing so much work that the local programs are afraid it will halt and catch fire. This would cause serious issues for the rest of the computer, so you head in and see what you can do.

The code it's running seems to be a variant of the kind you saw recently on that tablet. The general functionality seems very similar, but some of the instructions are different:

set X Y sets register X to the value of Y.
sub X Y decreases register X by the value of Y.
mul X Y sets register X to the result of multiplying the value contained in register X by the value of Y.
jnz X Y jumps with an offset of the value of Y, but only if the value of X is not zero. (An offset of 2 skips the next instruction, an offset of -1 jumps to the previous instruction, and so on.)
Only the instructions listed above are used. The eight registers here, named a through h, all start at 0.

The coprocessor is currently set to some kind of debug mode, which allows for testing, but prevents it from doing any meaningful work.

If you run the program (your puzzle input), how many times is the mul instruction invoked?
	*/
	/*
W $$Part1^YUVGZ201723(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC
	new (FileName,RecordsArr)
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set IP=1,total=0
	for I=$ascii("a"):1:$ascii("h") set @$char(I)=0
	for  {
		quit:'$data(RecordsArr(IP))
		set Command=RecordsArr(IP)
		set Op=$piece(Command," ",1),val1=$piece(Command," ",2),val2=$piece(Command," ",3)
		if Op="set" {
			set:val2?1L val2=@val2
			set @val1=val2
			set IP=IP+1
			continue
		}
		if Op="sub" {
			set:val2?1L val2=@val2
			set @val1=@val1-val2
			set IP=IP+1
			continue
		}
		if Op="mul" {
			set:val2?1L val2=@val2
			set @val1=@val1*val2
			set IP=IP+1,total=total+1
			continue
		}
		if Op="jnz" {
			set:val1?1L val1=@val1
			set:val2?1L val2=@val2
			if val1 {
				set IP=IP+val2
				continue
			}
			set IP=IP+1
			continue
		}
	}
	set ANS=total
	//
	//
	//
	quit ANS
	/*
Year 2017, Day 23 part 2:
Now, it's time to fix the problem.

The debug mode switch is wired directly to register a. You flip the switch, which makes register a now start at 1 when the program is executed.

Immediately, the coprocessor begins to overheat. Whoever wrote this program obviously didn't choose a very efficient implementation. You'll need to optimize the program if it has any hope of completing before Santa needs that printer working.

The coprocessor's ultimate goal is to determine the final value left in register h once the program completes. Technically, if it had that... it wouldn't even need to run the program.

After setting register a to 1, if the program were to run to completion, what value would be left in register h?
	*/
	/*
W $$Part2^YUVGZ201723(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC
	new (FileName,RecordsArr)
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	//By analayzing the code:
	//1. Value (in register b) starting from line3*line5-line6
	//2. Value stop when reaching register c by lines28-30, and register c initialized the stop value at lines7,8 so c=b-line8
	//3. Step size is being checked at line 31
	set from=$piece(RecordsArr(1)," ",3)*$piece(RecordsArr(5)," ",3)-$piece(RecordsArr(6)," ",3)
	set to=from-$piece(RecordsArr(8)," ",3)
	set step=-$piece(RecordsArr(31)," ",3)
	set h=0
	for i=from:step:to {
		for j=2:1:$piece(i**.5,".",1)+1 {
			if '(i#j) {
				set h=h+1
				quit
			}
		}
	}
	set ANS=h
	//
	//
	//
	quit ANS
CheckData(RecordsArr)
{
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		if $length(Record," ")'=3 {
			set Error="Wrong data format at line "_Line
			quit
		}
		set Op=$piece(Record," ",1),val1=$piece(Record," ",2),val2=$piece(Record," ",3)
		if ",set,sub,mul,jnz,"'[(","_Op_",") {
			set Error="Wrong data format at line "_Line
			quit
		}
		if Op'="jnz",val1'?1L {
			set Error="Wrong data format at line "_Line
			quit
		}
		if Op="jnz",val1'?1L,val1'?.1"-"1.N {
			set Error="Wrong data format at line "_Line
			quit
		}
		if val2'?1L,val2'?.1"-"1.N {
			set Error="Wrong data format at line "_Line
			quit
		}
		if val1?1L,",a,b,c,d,e,f,g,h,"'[(","_val1_",") {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
