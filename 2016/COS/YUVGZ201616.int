ROUTINE YUVGZ201616 [Type=INT]
YUVGZ201616	;https://adventofcode.com/2016/day/16;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
Input data:
00101000101111010
	*/
	/*
Year 2016, Day 16 part 1:
--- Day 16: Dragon Checksum ---
You're done scanning this part of the network, but you've left traces of your presence. You need to overwrite some disks with random-looking data to cover your tracks and update the local security system with a new checksum for those disks.

For the data to not be suspicious, it needs to have certain properties; purely random data will be detected as tampering. To generate appropriate random data, you'll need to use a modified dragon curve.

Start with an appropriate initial state (your puzzle input). Then, so long as you don't have enough data yet to fill the disk, repeat the following steps:

Call the data you have at this point "a".
Make a copy of "a"; call this copy "b".
Reverse the order of the characters in "b".
In "b", replace all instances of 0 with 1 and all 1s with 0.
The resulting data is "a", then a single 0, then "b".
For example, after a single step of this process,

1 becomes 100.
0 becomes 001.
11111 becomes 11111000000.
111100001010 becomes 1111000010100101011110000.
Repeat these steps until you have enough data to fill the desired disk.

Once the data has been generated, you also need to create a checksum of that data. Calculate the checksum only for the data that fits on the disk, even if you generated more data than that in the previous step.

The checksum for some given data is created by considering each non-overlapping pair of characters in the input data. If the two characters match (00 or 11), the next checksum character is a 1. If the characters do not match (01 or 10), the next checksum character is a 0. This should produce a new string which is exactly half as long as the original. If the length of the checksum is even, repeat the process until you end up with a checksum with an odd length.

For example, suppose we want to fill a disk of length 12, and when we finally generate a string of at least length 12, the first 12 characters are 110010110100. To generate its checksum:

Consider each pair: 11, 00, 10, 11, 01, 00.
These are same, same, different, same, different, same, producing 110101.
The resulting string has length 6, which is even, so we repeat the process.
The pairs are 11 (same), 01 (different), 01 (different).
This produces the checksum 100, which has an odd length, so we stop.
Therefore, the checksum for 110010110100 is 100.

Combining all of these steps together, suppose you want to fill a disk of length 20 using an initial state of 10000:

Because 10000 is too short, we first use the modified dragon curve to make it longer.
After one round, it becomes 10000011110 (11 characters), still too short.
After two rounds, it becomes 10000011110010000111110 (23 characters), which is enough.
Since we only need 20, but we have 23, we get rid of all but the first 20 characters: 10000011110010000111.
Next, we start calculating the checksum; after one round, we have 0111110101, which 10 characters long (even), so we continue.
After two rounds, we have 01100, which is 5 characters long (odd), so we are done.
In this example, the correct checksum would therefore be 01100.

The first disk you have to fill has length 272. Using the initial state in your puzzle input, what is the correct checksum?
	*/
	/*
W $$Part1^YUVGZ201616(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set a=RecordsArr(1),MAXLEN=272
	for  {
		quit:$length(a)>=MAXLEN
		set b=$translate($reverse(a),10,"01"),a=a_0_b
	}
	//write !,a
	
	set ANS=$$CheckSum($extract(a,1,MAXLEN))
	//
	//
	//
	quit ANS
}
CheckSum(num)
{
	for  {
		set ans=""
		for i=1:2:$length(num)-1 {
			set ans=ans_$select($extract(num,i)=$extract(num,i+1):1,1:0)
		}
		quit:$length(ans)#2
		set num=ans
	}
	quit ans
}
	/*
Year 2016, Day 16 part 2:
The second disk you have to fill has length 35651584. Again using the initial state in your puzzle input, what is the correct checksum for this disk?
	*/
	/*
W $$Part2^YUVGZ201616(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set data=RecordsArr(1),MAXLEN=35651584
	set checksum=MAXLEN,size=$length(data)
	//w !,data,!!!
	//
	//Let's say that string is s and not(string) is n then new strings are as follow:
	//1. s0n
	//2. s0n0s1n
	//3. s0n0s1n0s0n1s1n
	// .
	//n. sxnxsxnxsxn...xsxn
	//so any future string is in the repeating format of sxn
	//so after we got s (from our puzzle) we compute n and then we need to compute x which is changing.
	//Couldn't find a formula for x so computed it and rewrote the checksum computation (For you who found out how to compute this x, please let me know how)
	//The checksum is based on how many bits we need to compress into a single checksum bit.
	//Computing this and then we can compute checksum in chuncks of data
	//
	for loops=0:1 quit:checksum#2  set checksum=checksum/2
	set sizeForChecksum=2**loops
	for i=0:1 quit:2**i*size>=MAXLEN
	set a=0 for  quit:2**i-1<=$length(a)  set b=$translate($reverse(a),10,"01"),a=a_0_b
	set checksumbits=a_0
	write !,$l(checksumbits),!,sizeForChecksum,!
	set reva=$translate($reverse(data),10,"01")
	set tempchecksum="",starti=1,prefix=""
	for i=1:1:MAXLEN/sizeForChecksum {
		set temp=prefix
		for j=starti:1 {
			set td=data
			set:'(j#2) td=reva
			set temp=temp_td_$extract(checksumbits,j)
			quit:$length(temp)>=sizeForChecksum
		}
		if $length(temp)>=sizeForChecksum {
			w !,$length(temp)
			//w !,$extract(temp,1,sizeForChecksum),!,tempchecksum
			set tempchecksum=tempchecksum_$$CheckSum($extract(temp,1,sizeForChecksum))
			set prefix=$extract(temp,sizeForChecksum+1,*),starti=j+1
		}
	}
	set ANS=tempchecksum
	//
	//
	//
	quit ANS
}
CheckData(RecordsArr)
{
	if ($order(RecordsArr(""))'=1)||($order(RecordsArr(""),-1)'=1) {
		set Error="Only one line of data allowed"
		write !,Error
		quit 0
	}
	set Error="",Line=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		if $length($translate(Record,10)) {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
