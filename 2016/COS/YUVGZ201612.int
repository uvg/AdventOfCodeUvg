ROUTINE YUVGZ201612 [Type=INT]
YUVGZ201612	;https://adventofcode.com/2016/day/12;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
Input data:
cpy 1 a
cpy 1 b
cpy 26 d
jnz c 2
jnz 1 5
cpy 7 c
inc d
dec c
jnz c -2
cpy a c
inc a
dec b
jnz b -2
cpy c b
dec d
jnz d -6
cpy 16 c
cpy 12 d
inc a
dec d
jnz d -2
dec c
jnz c -5
	*/
	/*
Year 2016, Day 12 part 1:
--- Day 12: Leonardo's Monorail ---
You finally reach the top floor of this building: a garden with a slanted glass ceiling. Looks like there are no more stars to be had.

While sitting on a nearby bench amidst some tiger lilies, you manage to decrypt some of the files you extracted from the servers downstairs.

According to these documents, Easter Bunny HQ isn't just this building - it's a collection of buildings in the nearby area. They're all connected by a local monorail, and there's another building not far from here! Unfortunately, being night, the monorail is currently not operating.

You remotely connect to the monorail control systems and discover that the boot sequence expects a password. The password-checking logic (your puzzle input) is easy to extract, but the code it uses is strange: it's assembunny code designed for the new computer you just assembled. You'll have to execute the code and get the password.

The assembunny code you've extracted operates on four registers (a, b, c, and d) that start at 0 and can hold any integer. However, it seems to make use of only a few instructions:

cpy x y copies x (either an integer or the value of a register) into register y.
inc x increases the value of register x by one.
dec x decreases the value of register x by one.
jnz x y jumps to an instruction y away (positive means forward; negative means backward), but only if x is not zero.
The jnz instruction moves relative to itself: an offset of -1 would continue at the previous instruction, while an offset of 2 would skip over the next instruction.

For example:

cpy 41 a
inc a
inc a
dec a
jnz a 2
dec a
The above code would set register a to 41, increase its value by 2, decrease its value by 1, and then skip the last dec a (because a is not zero, so the jnz a 2 skips it), leaving register a at 42. When you move past the last instruction, the program halts.

After executing the assembunny code in your puzzle input, what value is left in register a?
	*/
	/*
W $$Part1^YUVGZ201612(FN)
	*/
Part1(FileName="",RecordsArr) [a,b,c,d] PUBLIC
{
	new a,b,c,d
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set ANS=$$Compute(0,0,0,0,.RecordsArr)
	//
	//
	//
	quit ANS
}
Compute(a,b,c,d,&Code) [a,b,c,d]
{
	set ip=1
	while $data(Code(ip)) {
		set command=Code(ip)
		//w !,command
		set instruct=$piece(command," ",1),val1=$piece(command," ",2),val2=$piece(command," ",3)
		if instruct="cpy" {
			set:val1'?.1"-"1.n val1=@val1
			set @val2=val1
			set ip=ip+1
		}
		if instruct="inc" {
			set @val1=@val1+1
			set ip=ip+1
		}
		if instruct="dec" {
			set @val1=@val1-1
			set ip=ip+1
		}
		if instruct="jnz" {
			set:val1'?.1"-"1.n val1=@val1
			if val1 {
				set ip=ip+val2
			}
			else {
				set ip=ip+1
			}
		}
	}
	quit a
}

	/*
Year 2016, Day 12 part 2:
As you head down the fire escape to the monorail, you notice it didn't start; register c needs to be initialized to the position of the ignition key.

If you instead initialize register c to be 1, what value is now left in register a?
	*/
	/*
W $$Part2^YUVGZ201612(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set ANS=$$Compute(0,0,1,0,.RecordsArr)
	//
	//
	//
	quit ANS
}
CheckData(RecordsArr)
{
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		set pieces=$length(Record," ")
		if pieces'=2,pieces'=3 {
			set Error="Wrong data format at line "_Line
			quit
		}
		set instruction=$piece(Record," ",1),val1=$piece(Record," ",2),val2=$piece(Record," ",3)
		if ",cpy,inc,dec,jnz,"'[(","_instruction_",") {
			set Error="Unknown instruction at line "_Line
			quit
		}
		if ",cpy,jnz,"[(","_instruction_",") {
			if pieces'=3 {
				set Error="Unknown "_instruction_" format at line "_Line
				quit
			}
			if val1'?1(1(1"a",1"b",1"c",1"d"),.1"-"1.N) {
				set Error="Unknown "_instruction_" format at line "_Line
				quit
			}
			if instruction="cpy",",a,b,c,d,"'[(","_val2_",") {
				set Error="Unknown registry "_val2_" at line "_Line
			}
			if instruction="jnz",val2'?.1"-"1.N {
				set Error="Unknown "_instruction_" format at line "_Line
				quit
			}
		}
		if ",inc,dec,"[(","_instruction_",") {
			if pieces'=2 {
				set Error="Unknown "_instruction_" format at line "_Line
				quit
			}
			if ",a,b,c,d,"'[(","_val1_",") {
				set Error="Unknown registry "_val1_" at line "_Line
			}			
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
