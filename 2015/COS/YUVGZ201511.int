ROUTINE YUVGZ201511 [Type=INT]
YUVGZ201511	;https://adventofcode.com/2015/day/11;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
hepxcrrq
	*/
	/*
Santa's previous password expired, and he needs help choosing a new one.

To help him remember his new password after the old one expires, Santa has devised a method of coming up with a password based on the previous one. Corporate policy dictates that passwords must be exactly eight lowercase letters (for security reasons), so he finds his new password by incrementing his old password string repeatedly until it is valid.

Incrementing is just like counting with numbers: xx, xy, xz, ya, yb, and so on. Increase the rightmost letter one step; if it was z, it wraps around to a, and repeat with the next letter to the left until one doesn't wrap around.

Unfortunately for Santa, a new Security-Elf recently started, and he has imposed some additional password requirements:

Passwords must include one increasing straight of at least three letters, like abc, bcd, cde, and so on, up to xyz. They cannot skip letters; abd doesn't count.
Passwords may not contain the letters i, o, or l, as these letters can be mistaken for other characters and are therefore confusing.
Passwords must contain at least two different, non-overlapping pairs of letters, like aa, bb, or zz.
For example:

hijklmmn meets the first requirement (because it contains the straight hij) but fails the second requirement requirement (because it contains i and l).
abbceffg meets the third requirement (because it repeats bb and ff) but fails the first requirement.
abbcegjk fails the third requirement, because it only has one double letter (bb).
The next password after abcdefgh is abcdffaa.
The next password after ghijklmn is ghjaabcc, because you eventually skip all the passwords that start with ghi..., since i is not allowed.
Given Santa's current password (your puzzle input), what should his next password be?
	*/
Part1(FileName="",Data="") PUBLIC
{
	if '$length(Data) {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit ""
		}
		quit:'$$LoadFile(FileName,.RecordsArr) ""
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit ""
		}
		if ($order(RecordsArr(""))'=1)||($order(RecordsArr(""),-1)'=1) {
			write !,"More than 1 lines of data in "_FileName
			quit ""
		}
		set Data=RecordsArr(1)
	}
	if Data'?8l {
		write !,"Must be exactly 8 lowercase letters"
		quit ""
	}
	set String=Data,NewPassword=""
	set NewPassword=$$GetNewPassword(String,1)
	quit NewPassword
}
GetNewPassword(Password,Location)
{
	if $$IsValidPassword(Password) return Password
	quit:Location<1 ""
	quit:Location>8 ""
	for  {
		set NewPassword=$$GetNewPassword(.Password,Location+1)
		if $length(NewPassword) return NewPassword
		set Letter=$ascii(Password,Location)+1
		if Letter>122 set Letter=97
		set $extract(Password,Location)=$char(Letter)
		if $$IsValidPassword(Password) return Password
		if Letter=97 return ""
	}
	q ""
}
IsValidPassword(Password)
{
	quit:Password'?8l 0
	if $length($translate(Password,"iol"))'=$length(Password) quit 0
	set (SW2,SW3)=0
	for i=1:1:7 {
		if $ascii(Password,i)+1=$ascii(Password,i+1),$ascii(Password,i)+2=$ascii(Password,i+2) set SW3=1
		if $extract(Password,i)=$extract(Password,i+1) set SW2($extract(Password,i))=""
	}
	quit:'SW3 0
	if $order(SW2(""))'=$order(SW2(""),-1) quit 1
	quit 0
}
	/*
Santa's password expired again. What's the next one?
	*/
Part2(FileName="",Data="") PUBLIC
{
	set NewPassword=$$Part1(.FileName,.Data)
	quit:'$length(NewPassword) ""
	set $extract(NewPassword,8)=$char($ascii(NewPassword,8)+1)
	quit $$GetNewPassword(NewPassword,1)
}

LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::2 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		if $length(Record) set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $system.Process.SetZEOF(0)
	quit 1
}

