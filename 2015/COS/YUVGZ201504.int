ROUTINE YUVGZ201504 [Type=INT]
YUVGZ201504	;https://adventofcode.com/2015/day/4;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
bgvyzdsv
	*/
	/*
Santa needs help mining some AdventCoins (very similar to bitcoins) to use as gifts for all the economically forward-thinking little girls and boys.

To do this, he needs to find MD5 hashes which, in hexadecimal, start with at least five zeroes. The input to the MD5 hash is some secret key (your puzzle input, given below) followed by a number in decimal. To mine AdventCoins, you must find Santa the lowest positive number (no leading zeroes: 1, 2, 3, ...) that produces such a hash.

For example:

If your secret key is abcdef, the answer is 609043, because the MD5 hash of abcdef609043 starts with five zeroes (000001dbbfa...), and it is the lowest such number to do so.
If your secret key is pqrstuv, the lowest number it combines with to make an MD5 hash starting with five zeroes is 1048970; that is, the MD5 hash of pqrstuv1048970 looks like 000006136ef....
	*/
Part1(FileName="",Data="") PUBLIC
{
	if '$length(Data) {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
		if ($order(RecordsArr(""))'=1)||($order(RecordsArr(""),-1)'=1) {
			write !,"Wrong data was red from "_FileName
			quit 0
		}
		set Data=RecordsArr(1)
	}
	quit:'$$CheckData(Data) 0
	for number=1:1 {
		set hash=$system.Encryption.MD5Hash(Data_number)
		if $ascii(hash,1)=0,$ascii(hash,2)=0,$ascii(hash,3)<16 quit
	}
	quit number
}
	/*
Now find one that starts with six zeroes.
	*/
Part2(FileName="",Data="") PUBLIC
{
	if '$length(Data) {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
		if ($order(RecordsArr(""))'=1)||($order(RecordsArr(""),-1)'=1) {
			write !,"Wrong data was red from "_FileName
			quit 0
		}
		set Data=RecordsArr(1)
	}
	quit:'$$CheckData(Data) 0
	for number=1:1 {
		set hash=$system.Encryption.MD5Hash(Data_number)
		if $ascii(hash,1)=0,$ascii(hash,2)=0,$ascii(hash,3)=0 quit
	}
	quit number
}
CheckData(Data)
{
	if '$length(Data) {
		write !,"Missing data"
		quit 0
	}
	quit 1
}

LoadFile(Filename,LineArr)
{
	kill LineArr
	set eofSW=$zutil(68,40,1)
	open Filename::2 quit:'$test 0
	use Filename for Counter=1:1 read Line quit:$zeof  set:$length(Line) LineArr(Counter)=Line
	close Filename
	if 'eofSW do $zutil(68,40,0)
	quit 1
}
