ROUTINE YUVGZ202114 [Type=INT]
YUVGZ202114	;https://adventofcode.com/2021/day/14;
	//Intersystems developer community id: 130669-ab1f69bf
	quit
	/*
Input data:
SNVVKOBFKOPBFFFCPBSF

HH -> P
CH -> P
HK -> N
OS -> N
HV -> S
VC -> C
VO -> K
OC -> C
FB -> S
NP -> S
OK -> H
OO -> N
PP -> B
VK -> B
BV -> N
PN -> K
HC -> C
NS -> K
BO -> C
BN -> O
SP -> H
FK -> K
KF -> N
VP -> H
NO -> N
OH -> N
CC -> O
PK -> P
BF -> K
CP -> N
SH -> V
VS -> P
BH -> B
KS -> H
HB -> K
BK -> S
KV -> C
SF -> B
BB -> O
PC -> S
HN -> S
FP -> S
PH -> C
OB -> O
FH -> K
CS -> P
OF -> N
FF -> V
PV -> B
PF -> C
FC -> S
KC -> O
PS -> V
CO -> F
CK -> O
KH -> H
OP -> O
SK -> S
VB -> P
FN -> H
FS -> P
FV -> N
HP -> O
SB -> N
VN -> V
KK -> P
KO -> V
BC -> B
FO -> H
OV -> H
CF -> H
HF -> K
SS -> V
SC -> N
CB -> B
SV -> C
SN -> P
PB -> B
KP -> S
PO -> B
CN -> F
ON -> B
CV -> S
HO -> O
NF -> F
VH -> P
NN -> S
HS -> S
NV -> V
NH -> C
NB -> B
SO -> K
NC -> C
VF -> B
BS -> V
VV -> N
BP -> P
KN -> C
NK -> O
KB -> F
	*/
	/*
Day 14 part 1:
--- Day 14: Extended Polymerization ---
The incredible pressures at this depth are starting to put a strain on your submarine. The submarine has polymerization equipment that would produce suitable materials to reinforce the submarine, and the nearby volcanically-active caves should even have the necessary input elements in sufficient quantities.

The submarine manual contains instructions for finding the optimal polymer formula; specifically, it offers a polymer template and a list of pair insertion rules (your puzzle input). You just need to work out what polymer would result after repeating the pair insertion process a few times.

For example:

NNCB

CH -> B
HH -> N
CB -> H
NH -> C
HB -> C
HC -> B
HN -> C
NN -> C
BH -> H
NC -> B
NB -> B
BN -> B
BB -> N
BC -> B
CC -> N
CN -> C
The first line is the polymer template - this is the starting point of the process.

The following section defines the pair insertion rules. A rule like AB -> C means that when elements A and B are immediately adjacent, element C should be inserted between them. These insertions all happen simultaneously.

So, starting with the polymer template NNCB, the first step simultaneously considers all three pairs:

The first pair (NN) matches the rule NN -> C, so element C is inserted between the first N and the second N.
The second pair (NC) matches the rule NC -> B, so element B is inserted between the N and the C.
The third pair (CB) matches the rule CB -> H, so element H is inserted between the C and the B.
Note that these pairs overlap: the second element of one pair is the first element of the next pair. Also, because all pairs are considered simultaneously, inserted elements are not considered to be part of a pair until the next step.

After the first step of this process, the polymer becomes NCNBCHB.

Here are the results of a few steps using the above rules:

Template:     NNCB
After step 1: NCNBCHB
After step 2: NBCCNBBBCBHCB
After step 3: NBBBCNCCNBBNBNBBCHBHHBCHB
After step 4: NBBNBNBBCCNBCNCCNBBNBBNBBBNBBNBBCBHCBHHNHCBBCBHCB
This polymer grows quickly. After step 5, it has length 97; After step 10, it has length 3073. After step 10, B occurs 1749 times, C occurs 298 times, H occurs 191 times, and N occurs 865 times; taking the quantity of the most common element (B, 1749) and subtracting the quantity of the least common element (H, 161) produces 1749 - 161 = 1588.

Apply 10 steps of pair insertion to the polymer template and find the most and least common elements in the result. What do you get if you take the quantity of the most common element and subtract the quantity of the least common element?
	*/
	/*
W $$Part1^YUVGZ202114(FN)
	*/
Part1(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Template=RecordsArr(1)
	set Answer="",Line=1
	for {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line) continue:Record=""
		set From=$piece(Record," -> ",1),To=$piece(Record," -> ",2)
		set PairArr(From)=To
	}
	write !,Template
	for i=1:1:10 {
		set NewTemplate=""
		for j=1:1:$length(Template)-1 {
			set From=$extract(Template,j,j+1)
			set To=PairArr(From)
			set NewTemplate=NewTemplate_$extract(Template,j)_To
		}
		set NewTemplate=NewTemplate_$extract(Template,*)
		write !,i,":  ",$length(NewTemplate)
		set Template=NewTemplate
	}
	for i=1:1:$length(Template) if $increment(Occur($extract(Template,i)))
	write !! zwrite Occur
	set Min=999999999999999999,Max=-1
	set Element=""
	for  {
		set Element=$order(Occur(Element)) quit:Element=""
		if Occur(Element)>Max set Max=Occur(Element)
		if Occur(Element)<Min set Min=Occur(Element)
	}
	set Answer=Max-Min
	
	quit Answer
}
	/*
Day 14 part 2:
--- Part Two ---
The resulting polymer isn't nearly strong enough to reinforce the submarine. You'll need to run more steps of the pair insertion process; a total of 40 steps should do it.

In the above example, the most common element is B (occurring 2192039569602 times) and the least common element is H (occurring 3849876073 times); subtracting these produces 2188189693529.

Apply 40 steps of pair insertion to the polymer template and find the most and least common elements in the result. What do you get if you take the quantity of the most common element and subtract the quantity of the least common element?
	*/
	/*
W $$Part2^YUVGZ202114(FN)
	*/
Part2(FileName="",RecordsArr) PUBLIC
{
	if $data(RecordsArr)'=10 {
		if '$length(FileName) {
			read !,"Enter filename: ",FileName
			write !
		}
		if '$length(FileName) {
			write !,"Missing file to read from"
			quit 0
		}
		quit:'$$LoadFile(FileName,.RecordsArr) 0
		if '$data(RecordsArr) {
			write !,"No data was red from "_FileName
			quit 0
		}
	}
	quit:'$$CheckData(.RecordsArr) 0
	set Template=RecordsArr(1)
	set Answer="",Line=1
	for {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line) continue:Record=""
		set From=$piece(Record," -> ",1),To=$piece(Record," -> ",2)
		set PairArr(From)=To
		set Occur($extract(From,1))=0
		set Occur($extract(From,2))=0
		set Occur(To)=0
	}
	for j=1:1:$length(Template) {
		set Occur($extract(Template,j))=Occur($extract(Template,j))+1
	}
	for j=1:1:$length(Template)-1 if $increment(DoubleArr($extract(Template,j,j+1)))
	//zw Template,Occur,DoubleArr
	for i=1:1:40 {
		kill NewDoubleArr merge NewDoubleArr=DoubleArr
		set From="" for  {
			set From=$order(DoubleArr(From)) quit:From=""
			set count=DoubleArr(From)
			continue:'count
			set To=PairArr(From),Occur(To)=Occur(To)+count
			set NewDoubleArr(From)=NewDoubleArr(From)-count
			set NewDoubleArr($extract(From,1)_To)=$get(NewDoubleArr($extract(From,1)_To))+count
			set NewDoubleArr(To_$extract(From,2))=$get(NewDoubleArr(To_$extract(From,2)))+count
		}
		kill DoubleArr merge DoubleArr=NewDoubleArr
	}
	//zw DoubleArr,Occur
	set Min=999999999999999999,Max=-1
	set Element=""
	for  {
		set Element=$order(Occur(Element)) quit:Element=""
		if Occur(Element)>Max set Max=Occur(Element)
		if Occur(Element)<Min set Min=Occur(Element)
	}
	set Answer=Max-Min
	
	quit Answer
}
CheckData(RecordsArr)
{
	set Line="",Error=""
	for  {
		set Line=$order(RecordsArr(Line)) quit:Line=""
		set Record=RecordsArr(Line)
		if Record'?.E {
			set Error="Wrong data format at line "_Line
			quit
		}
	}
	if $length(Error) {
		write !,Error
		quit 0
	}
	quit 1
}
LoadFile(FileName,RecordsArr)
{
	kill RecordsArr
	open FileName::.1 if '$test {
		write !,"Error loading file "_FileName
		quit 0
	}
	set EndOfFileSW=$zutil(68,40,1) //$system.Process.SetZEOF(1)
	use FileName
	for Line=1:1 {
		read Record quit:$zeof
		set RecordsArr(Line)=Record
	}
	close FileName
	if 'EndOfFileSW do $zutil(68,40,0) //$system.Process.SetZEOF(0)
	quit 1
}
